#compdef _robo robo

function _robo {
    local context state state_descr line
    typeset -A opt_args

    _arguments -C \
        '--raw[output raw command help]' \
        '(-)'{-h,--help}'[display help message]' \
        '(-q --quiet)'{-q,--quiet}'[quiet]' \
        '(-)'{-V,--version}'[display version]' \
        '(--no-ansi)--ansi[force ANSI output]' \
        '(--ansi)--no-ansi[disable ANSI output]' \
        {-n,--no-interaction}'[no interaction]' \
        '--simulate[show what would happen]' \
        '(-vv -vvv --verbose)-v[normal verbosity]' \
        '(-v -vvv --verbose)-vv[more verbose]' \
        '(-v -vv)'{-vvv,--verbose}'[debug]' \
        '--format=[output format]:format:->formats' \
        '--progress-delay=[number of seconds to show progress bar]' \
        '1: :_robo_get_command_list' \
        '2: :->commands' \
        '*::arg:->args'

    case "$state" in
        formats)
            _values 'formats' txt xml json md
            ;;
        commands)
            case $line[1] in
                help)
                    _robo_get_command_list
                ;;
                *)
                    _files
                ;;
            esac
            ;;
        args)
            _files
            ;;
    esac
}

function _robo_does_command_list_need_generating {
    [[ ! -f .robocommandcache ]] || [[ RoboFile.php -nt .robocommandcache ]]
}	

function _robo_get_command_list {
    if type robo &> /dev/null; then
        if _robo_does_command_list_need_generating; then
            local hline
            local start_list
            local -a cmdlist
            robo list | while read -A hline; do
                if [[ ${(j: :)hline} = "Available commands:" ]] then;
                    start_list=1
                    continue
                fi
                [[ start_list -ne 1 ]] && continue
                cmdlist=($cmdlist "${hline[1]//:/\\:}:${hline[2,-1]//:/\\:}")
            done
        fi
        _describe -t robo-commands 'Robo command' cmdlist
    fi
}
