#compdef _robo robo

# Zsh completion script for Robo.
#
# To make things better:
# zstyle ':completion:*:*:robo::' use-cache on
# zstyle ':completion:*:*:robo::' accept-exact '*(N)'
# zstyle ':completion:*:*:robo::' completer _complete _approximate

function _robo {
  local curcontext="$curcontext" state state_descr line
  integer ret=1
  typeset -A opt_args

  _arguments -C \
    '--raw[output raw command help]' \
    '(-)'{-h,--help}'[display help message]' \
    '(-q --quiet)'{-q,--quiet}'[quiet]' \
    '(-)'{-V,--version}'[display version]' \
    '(--no-ansi)--ansi[force ANSI output]' \
    '(--ansi)--no-ansi[disable ANSI output]' \
    {-n,--no-interaction}'[no interaction]' \
    '--simulate[show what would happen]' \
    '(-vv -vvv --verbose)-v[normal verbosity]' \
    '(-v -vvv --verbose)-vv[more verbose]' \
    '(-v -vv)'{-vvv,--verbose}'[debug]' \
    '--format=[output format]:format:->formats' \
    '--progress-delay=[number of seconds to show progress bar]' \
    '1: :->cmd' \
    '2: :->subcmd' \
    '*::arg:->args'

  zstyle -s ":completion:$curcontext:" cache-policy _robo_cache_policy

  case $state in
    formats)
      _values 'formats' txt xml json md && ret=0
      ;;
    cmd)
      _robo_get_command_list && ret=0
      ;;
    subcmd)
      case $line[1] in
        help)
          _robo_get_command_list && ret=0
          ;;
        *)
          _files && ret=0
          ;;
      esac
      ;;
    args)
      _files && ret=0
      ;;
  esac

  return ret
}

_robo_cache_policy() {
  typeset -a old

  # cache for a minute
  old=( "$1"(mm+10) )
  (( $#old )) && return 0

  return 1
}

#(( $+functions[_robo_does_command_list_need_generating] )) ||
function _robo_does_command_list_need_generating {
  [[ ! -f .robocommandcache ]] || [[ RoboFile.php -nt .robocommandcache ]]
}	

#(( $+functions[_robo_get_command_list] )) ||
function _robo_get_command_list {
  if type "robo" &> /dev/null && [[ -f RoboFile.php ]]; then
    local cacheid="robo:${PWD//\//_}"
    if _cache_invalid "$cacheid" || ! _retrieve_cache "$cacheid"; then
      setopt extendedglob
      local -a lines cmds
      lines=(${(f)"$(_call_program list-commands robo list 2>&1)"})
      lines=(${(M)${lines[$((${lines[(i)*commands:]} + 1)),-1]}:# *})
      cmds=(${${lines##([[:space:]]##)}%%[[:space:]]*})
      _store_cache "$cacheid" cmds
    fi
    compadd -X "==== Robo Commands ====" $cmds
  fi
}

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
